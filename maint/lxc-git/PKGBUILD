# Contributor: tuxce.net <tuxce.net@gmail.com>
# Maintainer: Jon Nordby <jononor@gmail.com>

pkgname=lxc-git
_pkgname=lxc
pkgver=20120210
pkgrel=1
pkgdesc="Linux Containers"
arch=('i686' 'x86_64')
url="http://lxc.sourceforge.net/"
license=('LGPL')
makedepends=('git')
provides=($_pkgname)
conflicts=($_pkgname)
source=('0001-Replace-pkglib_PROGRAMS-with-pkglibexec_PROGRAMS.patch')
md5sums=('4262ff1c78bece47f97876cb88f5d140')

_gitroot=git://lxc.git.sourceforge.net/gitroot/lxc/lxc
_gitname=lxc

build() {
  cd "$srcdir"

  msg "Connecting to $_gitroot..."
  if [ -d $_gitname ]; then
    cd $_gitname && git pull origin && cd ..
  else
    git clone $_gitroot
  fi

  rm -rf $_gitname-build
  cp -a $_gitname $_gitname-build

  cd $_gitname-build

  # Patch
  git am "$srcdir/0001-Replace-pkglib_PROGRAMS-with-pkglibexec_PROGRAMS.patch"

  # BUILD
  ./autogen.sh
  ./configure --prefix=/usr --libexecdir=/usr/bin --sysconfdir=/etc \
    --localstatedir=/var --disable-doc
  make
}

package() {
  cd $_gitname-build

  make DESTDIR="$pkgdir/" install
  install -d -m755 "$pkgdir/var/lib/lxc"
}

# vim:set ts=2 sw=2 et:
